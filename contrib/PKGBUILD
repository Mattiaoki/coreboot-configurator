# Maintainer: Bernhard Landauer <bernhard@manjaro.org>

pkgname=coreboot-configurator
pkgver=7.f
pkgrel=1
pkgdesc="A graphical interface to set options on devices with coreboot firmware"
arch=('x86_64')
url="https://github.com/StarLabsLtd/coreboot-configurator"
license=('GPL3')
depends=('nvramtool' 'qt5-base')
makedepends=('inkscape' 'meson' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('ac69e0b96447729021545d7f83dcb03411abebf557d5e57db463c063af946ba6')

prepare() {
  cd "$pkgname-$pkgver"
  find . -type f -exec sed -i 's|/usr/sbin|/usr/bin|g' {} \;
}

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  # There's probably a way to change the path before building
  install -d "$pkgdir/usr/bin/"
  mv "$pkgdir/usr/sbin/$pkgname" "$pkgdir/usr/bin/"
  rm -rf "$pkgdir/usr/sbin/"
}
